//Separate from the menus because you could put other interactions here, like poking or booping or punching or asking about various hotspots
OnMouseDoubleClick : all
{
	if reference5 == 0 //reference5 is the mouse button being clicked, in this case a left click
	{
		//reference3 is which character is being clicked
		//If you want to add double click interactions, reference4 will be the name of the hitbox the mouse is over
		if reference3 == 0
		{
			if !UserAcceptedRole
			{
				if ghostupmin < 5
				{
					if UserFainted
					{
						"\0\b[0]\s[3]Are you sure you're all right? You can take time to recuperate if you need. \n\n[half]\s[4]... Well. \s[1]I won't nag. "
					}
					else
					{
						"\0\b[0]\s[5]My, excited, aren't we?"
					}
				}
				elseif ghostupmin >= 60
				{
					"\0\b[0]\s[1]Ah, ready at last?"
				}
				else //Reasonable amount of time
				{
					"\0\b[0]\s[0]Ready to hear what I've come up with for you?\w8\w8\w8\w8"
				}
				"\n\n[half]\s[0]I'd like for you to become \_a[OnRole.Information]my assistant\_a. "
				"\n\n\_q"
				"\![*]\q[Yes,OnAcceptRole]  \![*]\q[No,OnNotAcceptRole]"
			}
			else
			{
				if reference4 == "orb"; OrbCheck
				elseif reference4 == "pen"; PenCheck
				else; OnSakuraMenu
			}
		}
	}
}

OnAcceptRole
{
	UserAcceptedRole = 1
	"\0\b[6]\s[7]Wonderful! \s[9]I look forward to working with you, my dear assistant."
}

OnNotAcceptRole
{
	"\0\b[0]\s[4]Ah... of course, you need more time. \n\n[half]\s[8]Not to worry. \s[1]The offer will still be here when you change your mind... \n\n[half]\s[5]Or we can always erase you. \s[7]It's entirely your decision."
}

OnRole.Information
{
	"\0\b[2]\s[7]As my assistant, you would help me in making decisions that influence what the next interation of your universe will be like. \n\n[half]\s[6]Everything from what color a particular flower should be, to how many stars will be born and die. \n\n[half]\s[0]Each decision can have innumerable consequences on how things play out. \s[1]I'd like to have a second opinion now and then. \n\n[half]\s[5]So? Will you accept your role as my assistant? \n\n\_q\![*]\q[Yes,OnAcceptRole]  \![*]\q[No,OnNotAcceptRole]"
}

OnMouseMove
{
	if reference4 != ""
	{
		stroke++
		
		if reference4 == "wings"
		{
			if stroke % 40 == 0
			{
				WingsCheck
			}
		}
		elseif reference4 == "skirt"
		{
			if stroke % 80 == 0
			{
				SkirtCheck
			}
		}
	}
}

OnMouseLeaveAll
{
	stroke = 0
}

WingsCheck : nonoverlap
{
	"\0\b[6]\s[4]Careful, my wings can be sharp at times."
	"\0\b[6]\s[3]Watch your fingers. When my feathers are brittle, the shards like to dig into whatever they touch."
	"\0\b[6]\s[8]Mind them, they bite."
	"\0\b[6]\s[6]That's nice... \s[1]Just don't cut yourself."
	"\0\b[6]\s[7]It's interesting to see grooming instincts at work."
}

SkirtCheck : nonoverlap
{
	"\0\b[6]\s[2]Hm? Oh, my legs? \s[0]I only bring them out when I need them."
	"\0\b[6]\s[8]...? There's only more dress under there, if that's what you're curious about."
	"\0\b[6]\s[4]I understand the curiosity, but really..."
	"\0\b[6]\s[6]I only walk when required. Floating is much easier."
	"\0\b[6]\s[0]Sometimes, I try having a tail to see what it's like."
}

OrbCheck : nonoverlap_pool
{
	//LastOrbClick[0] - timestamp for last click within 5 minutes
	//LastOrbClick[1] - number of times clicked in the last 5 minutes
	if GETSECCOUNT - LastOrbClick[0] > 300
	{
		LastOrbClick[0] = GETSECCOUNT
		LastOrbClick[1] = 1
	}
	else
	{
		LastOrbClick[1]++
	}
	
	if LastOrbClick[1] >= 5
	{
		"\0\b[6]\s[25]\n\f[height,150%]\f[align,center]That is \f[bold,1]enough\f[bold,default]."
	}
	else
	{
		"\0\b[6]\s[2]Careful! It's hot! You don't want to melt, do you?"
		"\0\b[6]\s[8]Watch your hands...! The slightest touch at this stage can have countless ramifications."
		"\0\b[6]\s[4]No, no, the cosmic nucleus does \f[italic,1]not\f[italic,default] need to be groomed."
		"\0\b[6]\s[25]%(username), you're throwing off my concentration."
		"\0\b[6]\s[3]Please don't get carried away. I'd rather not clean up your remains if you dissolve on contact."
	}
}

PenCheck : sequential
{
	"\0\b[6]\s[7]I write the laws of the universe with this."
	"\0\b[6]\s[5]My quill pen is made from one of my feathers. \s[9]Only one of them should exist at a time."
	"\0\b[6]\s[1]There's no erasing ink, so when I make a mistake, I have to embrace it."
	"\0\b[6]\s[0]The ink never runs out, because it isn't really ink. \s[6]It's made from the energy I'm infusing back into the universe."
	"\0\b[6]\s[8]I've actually lost pens before. Let me know if you find one."
}