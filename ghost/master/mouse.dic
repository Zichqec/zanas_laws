//Separate from the menus because you could put other interactions here, like poking or booping or punching or asking about various hotspots
OnMouseDoubleClick : all
{
	if reference5 == 0 //reference5 is the mouse button being clicked, in this case a left click
	{
		//reference3 is which character is being clicked
		//If you want to add double click interactions, reference4 will be the name of the hitbox the mouse is over
		if reference3 == 0
		{
			if !UserAcceptedRole
			{
				if ghostupmin < 5
				{
					if UserFainted
					{
						"Are you sure you're ready? You can take time to recuperate if you need."
					}
					else
					{
						"My, excited, aren't we?"
					}
				}
				elseif ghostupmin >= 60
				{
					"Ah, ready at last?"
				}
				else //Reasonable amount of time
				{
					"Are you ready to accept this role?"
				}
				"\n\n\_q"
				"\![*]\q[Yes,OnAcceptRole]  \![*]\q[No,OnNotAcceptRole]"
			}
			else
			{
				if reference4 == "orb"; OrbCheck
				elseif reference4 == "pen"; PenCheck
				else; OnSakuraMenu
			}
		}
	}
}

OnAcceptRole
{
	UserAcceptedRole = 1
	"great, you're my assistant now"
}

OnNotAcceptRole
{
	"hmmmmmm..."
}

OnMouseMove
{
	if reference4 != ""
	{
		stroke++
		
		if reference4 == "wings"
		{
			if stroke % 40 == 0
			{
				"\0\s[0]Careful, they are quite sharp"
			}
		}
		elseif reference4 == "skirt"
		{
			if stroke % 80 == 0
			{
				"\0\s[0]Hm? Oh, my legs? I only bring them out when I need them."
			}
		}
	}
}

OnMouseMove.Sakura
{
	if reference4 == "wings"
	{
		"\0\s[0]Petting??"
	}
	elseif reference4 == "skirt"
	{
		
	}
}

OnMouseLeaveAll
{
	stroke = 0
}

OrbCheck
{
	//LastOrbClick[0] - timestamp for last click within 5 minutes
	//LastOrbClick[1] - number of times clicked in the last 5 minutes
	if GETSECCOUNT - LastOrbClick[0] > 300
	{
		LastOrbClick[0] = GETSECCOUNT
		LastOrbClick[1] = 1
	}
	else
	{
		LastOrbClick[1]++
	}
	
	if LastOrbClick[1] >= 5
	{
		"Enough of that."
	}
	else
	{
		"Careful, it's hot, you'll melt!"
	}
}

PenCheck
{
	"I write the laws of the universe with it."
}