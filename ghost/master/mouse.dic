//Separate from the menus because you could put other interactions here, like poking or booping or punching or asking about various hotspots
OnMouseDoubleClick : all
{
	if reference5 == 0 //reference5 is the mouse button being clicked, in this case a left click
	{
		//reference3 is which character is being clicked
		//If you want to add double click interactions, reference4 will be the name of the hitbox the mouse is over
		if reference3 == 0
		{
			if !UserAcceptedRole
			{
				if ghostupmin < 5
				{
					if UserFainted
					{
						"\0\b[0]\s[3]Are you sure you're all right? You can take time to recuperate if you need. \n\n[half]\s[4]... Well. \s[1]I won't nag. "
					}
					else
					{
						"\0\b[0]\b[5]My, excited, aren't we?"
					}
				}
				elseif ghostupmin >= 60
				{
					"\0\b[0]\s[1]Ah, ready at last?"
				}
				else //Reasonable amount of time
				{
					"\0\b[0]\s[0]Ready to hear what I've come up with for you?\w8\w8\w8\w8"
				}
				"\n\n[half]\s[0]I'd like for you to become \_a[OnRole.Information]my assistant\_a. "
				"\n\n\_q"
				"\![*]\q[Yes,OnAcceptRole]  \![*]\q[No,OnNotAcceptRole]"
			}
			else
			{
				if reference4 == "orb"; OrbCheck
				elseif reference4 == "pen"; PenCheck
				else; OnSakuraMenu
			}
		}
	}
}

OnAcceptRole
{
	UserAcceptedRole = 1
	"\0\b[6]\s[7]Wonderful! \s[9]I look forward to working with you, my dear assistant."
}

OnNotAcceptRole
{
	"\0\b[0]\s[4]Ah... of course, you need more time. \n\n[half]\s[8]Not to worry. \s[1]The offer will still be here when you change your mind... \n\n[half]\s[5]Or we can always erase you. \s[7]It's entirely your decision."
}

OnRole.Information
{
	"\0\b[2]\s[7]As my assistant, you would help me in making decisions that influence what the next interation of your universe will be like. \n\n[half]\s[6]Everything from what color a particular flower should be, to how many stars should be born and die. \n\n[half]\s[0]Each decision can have innumerable consequences on how things play out. \s[1]I'd like to have a second opinion now and then. \n\n[half]\s[5]So? Will you accept your role as my assistant? \n\n\_q\![*]\q[Yes,OnAcceptRole]  \![*]\q[No,OnNotAcceptRole]"
}

OnMouseMove
{
	if reference4 != ""
	{
		stroke++
		
		if reference4 == "wings"
		{
			if stroke % 40 == 0
			{
				"\0\s[0]Careful, they are quite sharp"
			}
		}
		elseif reference4 == "skirt"
		{
			if stroke % 80 == 0
			{
				"\0\s[0]Hm? Oh, my legs? I only bring them out when I need them."
			}
		}
	}
}

OnMouseMove.Sakura
{
	if reference4 == "wings"
	{
		"\0\s[0]Petting??"
	}
	elseif reference4 == "skirt"
	{
		
	}
}

OnMouseLeaveAll
{
	stroke = 0
}

OrbCheck
{
	//LastOrbClick[0] - timestamp for last click within 5 minutes
	//LastOrbClick[1] - number of times clicked in the last 5 minutes
	if GETSECCOUNT - LastOrbClick[0] > 300
	{
		LastOrbClick[0] = GETSECCOUNT
		LastOrbClick[1] = 1
	}
	else
	{
		LastOrbClick[1]++
	}
	
	if LastOrbClick[1] >= 5
	{
		"Enough of that."
	}
	else
	{
		"Careful, it's hot, you'll melt!"
	}
}

PenCheck
{
	"I write the laws of the universe with it."
}