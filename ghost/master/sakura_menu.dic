SakuraMenuGreetings : nonoverlap
{
	"Hello."
	"How can I help you?"
}

OnSakuraMenu : all
{
	_lastgreeting = ""
	if SHIORI3FW.Eventid == "OnMouseDoubleClick"; _lastgreeting = SakuraMenuGreetings
	else; _lastgreeting = reference1
	if SHIORI3FW.Eventid != "OnMouseDoubleClick"; "\C\![lock,balloonrepaint]\c"
	
	"\0\s[0]\b[0]\![set,autoscroll,disable]\f[anchorvisitedfontcolor,default.anchor]"
	
	"\n\n\_q"
	
	"\![*]\q[Talk to me,OnAiTalk]"
	if LastTalk != ""; "  \![*]\q[Say that again,OnLastTalk]"
	"\n\n"
	
	_times = ("Off,0","30s,30","1m,60","3m,180","5m,300","10m,600")
	foreach _times; _time
	{
		switch TOINT(_time[1]) == aitalkinterval
		{
			"\__q[OnTalkRateChange,%(_time[1]),""%(_lastgreeting)""]%(_time[0])\__q"
			"\f[italic,1]\_a[OnTalkRateChange,%(_time[1]),""%(_lastgreeting)""]%(_time[0])\_a\f[italic,default]"
		}
		"  "
	}
	
	if UniverseReady
	{
		"\n\n"
		"\![*]\__q[OnNewUniverse]I'm ready,## send me to the new universe\__q"
	}
	
	"\n\n"
	"\![*]\q[Nevermind,blank]"
	
	if SHIORI3FW.DebugMode
	{
		"\n\n"
		
		"UserAcceptedRole:  \![*]\q[(%(UserAcceptedRole)),OnDebug.UserAcceptedRole,""%(_lastgreeting)""]\n"
		"QuestionsAsked: %(QuestionsAsked)\n"
		"TotalUniverses: %(TotalUniverses)\n"
		
		
		"\f[color,default]"
	}
	
	"\![unlock,balloonrepaint]"
	"\_l[0,0]"
	if SHIORI3FW.Eventid == "OnMouseDoubleClick"; "\![quicksection,false]"
	"%(_lastgreeting)"
}

OnLastTalk
{
	LastTalk
}

OnTalkRateChange
{
	aitalkinterval = TOINT(reference0)
	OnSakuraMenu
}

OnNewUniverse
{
	QuestionsAsked = 0
	TotalUniverses++
	
	"At last.\n\n Fade out.\n\n Fade in.\n\n How was it?"
}

OnDebug.UserAcceptedRole
{
	if UserAcceptedRole != 1; UserAcceptedRole = 1
	else; UserAcceptedRole = 0
	OnSakuraMenu
}