SakuraMenuGreetings : nonoverlap_pool
{
	if UniverseReady
	{
		"\s[7]Are you prepared to start the new universe?"
		"\s[0]Shall we begin?"
		"\s[5]You've done well. Ready to see the results?"
	}
	else
	{
		"\s[6]Things are coming along rather nicely, don't you think?"
		"\s[25]I'm still preparing... there is much work to do."
		"\s[1]Don't forget to rest if you need to. The work will be here when you're ready."
	}
}

OnSakuraMenu : all
{
	if SHIORI3FW.Eventid != "OnMouseDoubleClick"; "\C\![lock,balloonrepaint]\c"
	
	"\0\b[0]\s[0]\![set,autoscroll,disable]\![close,inputbox,OnSetName]\![set,scaling,100,100]\![set,alpha,100]"
	
	"\n\n\n\_q"
	
	if UniverseReady
	{
		"\![*]\__q[OnNewUniverse.Start]Begin Iteration\__q"
		"\n\n"
	}
	
	"\![*]\__q[OnAiTalk]Discuss\__q"
	if LastTalk != ""; "  \![*]\__q[OnLastTalk]Repeat\__q"
	"\n\n"
	
	"\![*]\__q[OnConfigMenu]Config\__q"
	"  "
	"\![*]\q[Cancel,blank]"
	
	"\![unlock,balloonrepaint]"
	"\_l[0,0]"
	if SHIORI3FW.Eventid == "OnMouseDoubleClick"; "\![quicksection,false]%(SakuraMenuGreetings)"
}

OnLastTalk
{
	LastTalk
}

OnNewUniverse
{
	QuestionsAsked = 0
	TotalUniverses++
	LastAnswers = AnsweredQuestions
	AnsweredQuestions = IARRAY
	OnPickChoices
	
	"\0\b[6]\s[25]\t\*So it is said, so it shall be.\s[25]\i[202,wait]\s[25]\i[102,wait]\cOur little universe... I cannot wait to see what you will become.\w8\w8\w8\w8\c\f[align,center]\f[height,150%]\![set,balloonwait,3]Be %(Capitalize(strong)).\w8\nBe Loved.\w8%(fadeoutlong)\c\b[-1]\w8\w8\w8\w8\w8\w8\w8\w8\s[0]\![set,balloonwait,1]\b[0]%(fadeintext('Easy now... lay down if you need to'))... \w8\w8\w8\n\n[half]I know, the information influx is a lot for your mind. \s[1]Don't push yourself too hard. \n\n\_q\![*]\__q[OnNewUniverse.Im_Okay]I'm okay,## thank you.\__q\n\![*]\__q[OnNewUniverse.What_Was_That]What was all that?\__q"
}

OnNewUniverse.Buttons
{
	"\n\n\_q\![*]\__q[OnNewUniverse.Questions]I have another question...\__q\n\![*]\__q[OnNewUniverse]I'm ready now.\__q"
}

OnNewUniverse.Start
{
	"\0\b[0]\s[7]\t\*Wonderful! I am excited to see how things will go this time, aren't you? \n\n[half]\s[0]We should see quite the change with these new parameters! \n\n\_q\![*]\__q[OnNewUniverse]Let's start.\__q\n\![*]\__q[OnNewUniverse.Questions]Actually,## I have some questions...\__q"
}

OnNewUniverse.Questions
{
	"\0\b[0]\s[8]\t\*What would you like to know? \n\n\_q\![*]\__q[OnNewUniverse.Questions_Go_Back]Why can't I go back?\__q\n\![*]\__q[OnNewUniverse.Questions_How_Many_Times]How many times have you done this?\__q\n\![*]\__q[OnNewUniverse.Questions_Be_Destroyed]Does the universe really have to be destroyed?\__q\n\![*]\__q[OnNewUniverse.Questions_Cancel]Can we wait a little longer?\__q\n\![*]\__q[OnNewUniverse]I'm ready to start.\__q"
}

OnNewUniverse.Questions_Go_Back : all
{
	"\0\b[4]\s[3]\t\*Souls are... fragile. \n\n[half]\s[4]I could return the material that makes up your physical body to the cosmic nucleus,%(br)even recreate your form exactly as I see it now. \n\n[half]However... \n\n[half]\s[3]There is an extremely low probability that your soul, and thus the parts of your mind that make you who you are, would survive the process. \n\n[half]\s[4]You would be gone, and a new soul created in your place. One without the experience of your past lives. \n\n[half]\s[1]One that is... not you. \n\n[half]\s[8]It may be selfish, but I don't want to take that risk. \s[3]Not until you're ready to be destroyed."
	OnNewUniverse.Buttons
}

OnNewUniverse.Questions_How_Many_Times : all
{
	"\0\b[0]\s[8]\t\*I... honestly can say that I've never thought to count! \n\n[half]\s[0]"
	if TotalUniverses == 0
	{
		"I \f[italic,1]do\f[italic,default] know that I'll be keeping track of how many we do together"
	}
	else
	{
		_s = "s"; if TotalUniverses == 1; _s = ""
		"I \f[italic,1]do\f[italic,default] know that we have completed the creation of %(TotalUniverses) iteration%(_s) of your universe together so far."
	}
	OnNewUniverse.Buttons
}

OnNewUniverse.Questions_Be_Destroyed : all
{
	"\0\b[2]\s[3]\t\*Technically speaking, no, we do not \f[italic,1]have\f[italic,default] to destroy the current iteration. \n\n[half]\s[4]However, once it runs out of the last of its energy, everything will stop. It may not be destruction per se, but it is still the functional end of all life. \n\n[half]\s[9]We destroy it in order to give new life to the universe, and all of the beings within it, once more. \n\n[half]\s[5]It is a very important duty, %(username)."
	OnNewUniverse.Buttons
}

OnNewUniverse.Questions_Cancel
{
	"\0\b[6]\s[1]Ah... Yes, of course. \n\n[half]\s[0]We'll try again whenever you're ready."
}

OnNewUniverse.Im_Okay
{
	"\0\b[0]\s[9]\t\*Good, good... You've done well, %(username). \n\n[half]\s[7]Be sure to rest if you need it. Do a bit of that... \s[8]drinking and eating business. \n\n[half]\s[5]Stretch or sleep if you need to. \n\n[half]\s[0]Then, we'll get started planning the next iteration.\x"
}

OnNewUniverse.What_Was_That
{
	"\0\b[2]\s[4]\t\*Well... I suppose it was 'everything', in a way. All of the new reality across the entirety of its timeline. \s[3]Though, how you process it is quite different from the angels... \n\n[half]\s[25]Whenever a new iteration is started, I am able to navigate all of space-time in a singular moment of infinity. You were designed to be able to percieve 4 dimensions of existance, but only interface with 3 of them. \n\n[half]\s[0]Which is to say, while you can see and experience time, you are unable to manipulate it. Your mind has to view it chronologically to understand it.\x\s[8]Even with that in mind, it's still quite a lot of information to take in at once... \s[3]Viewing an entire universe's timeline was likely too much for you to pick out more than a few broad strokes. \n\n[half]\s[5]Not to worry, though. I've made plenty of notes for us both. We'll be able to review every detail in our own time as we prepare for the next iteration.\x"
}

OnConfigMenu : all
{
	"\C\![lock,balloonrepaint]\c"
	"\0\b[2]\![set,autoscroll,disable]\_q\*\f[anchorvisitedfontcolor,default.anchor]\![no-autopause]"
	
	"Your name:\n"
	"\_a[OnChangeName]%(username)\_a  \f[color,disable]\__q[OnSetName,""%(SHIORI3FW.UserNameFull)""](Reset)\__q\f[color,default]"
	"\n\n"
	
	"Talkrate:\n"
	_times = ("Off,0","30s,30","1m,60","3m,180","5m,300","10m,600")
	foreach _times; _time
	{
		switch TOINT(_time[1]) == aitalkinterval
		{
			"\__q[OnTalkRateChange,%(_time[1])]%(_time[0])\__q"
			"\f[underline,1]\_a[OnTalkRateChange,%(_time[1])]%(_time[0])\_a\f[underline,default]"
		}
		"  "
	}
	"\n\n"
	_floating = "On"
	if !IsFloating; _floating = "Off"
	"Floating: \_a[OnToggleFloating]%(_floating)\_a"
	"\n"
	_sparkles = "Moving"
	if !MovingSparklies; _sparkles = "Static"
	"Sparkles: \_a[OnToggleSparklies]%(_sparkles)\_a"
	
	if SHIORI3FW.BalloonName == "Angelbox (Biblically Accurate)"
	{
		"\n\n"
		_ballooncolor = CurrentAngelbox[1]
		"\![*]Balloon color: \__q[OnBalloonCustomization]%(_ballooncolor)\__q"
	}
	
	"\n\n"
	"\![*]\q[Back,OnSakuraMenu]  \![*]\q[Close menu,blank]"
	"\n[0]\f[color,disable]\f[align,right]v%(ghostver)"
	"\![unlock,balloonrepaint]"
}

OnChangeName
{
	"\C\![open,inputbox,OnSetName,-1,""%(username)"",--INPUT_BALLOON]"
}

OnSetName
{
	_name = reference0
	if CUTSPACE(_name) != ""
	{
		nickname = SHIORI3FW.EscapeAllTags(_name)
	}
	OnConfigMenu
}

OnTalkRateChange
{
	aitalkinterval = TOINT(reference0)
	OnConfigMenu
}

OnToggleFloating
{
	"\C\![bind,Floating,On/Off]\![raise,OnConfigMenu]"
}

OnToggleSparklies
{
	"\C\![bind,Sparkles,Static]\![raise,OnConfigMenu]"
}

OnBalloonCustomization : all
{
	"\C\![lock,balloonrepaint]\c"
	"\0\b[4]\![set,autoscroll,disable]\_q\*\f[anchorvisitedfontcolor,default.anchor]\![no-autopause]"
	"\f[align,center]\f[bold,1]Defaults\f[bold,default]\n\f[align,left]"
	for _i = 0; _i < 4; _i++
	{
		_number = BalloonColors[_i][0]
		_name = BalloonColors[_i][1]
		
		if TOINT(CurrentAngelbox[0]) == _i; "\![*]"
		
		"\__q[OnPickColor,%(_number)]%(_name)\__q"
		
		//:melting_face: why is it like this
		if _i == 0; "\n"
		if _i == 1; "\n[0]\f[align,right]"
		elseif _i == 2; "\n[-100]\_l[0,]\f[align,right]"
	}
	"\n\n"
	"\f[align,center]\f[bold,1]Basic\f[bold,default]\n\f[align,left]"
	_col = 0
	for _i = 4; _i < 25; _i++
	{
		_number = BalloonColors[_i][0]
		_name = BalloonColors[_i][1]
		
		if TOINT(CurrentAngelbox[0]) == _i; "\![*]"
		"\__q[OnPickColor,%(_number)]%(_name)\__q"
		
		switch _col % 3
		{
			"\_l[0,]\n[0]\f[align,center]"
			"\n[0]\f[align,right]"
			"\n\f[align,left]"
		}
		_col++
	}
	"\n\n"
	"\f[align,center]\f[bold,1]Unique\f[bold,default]\n\f[align,left]"
	for _i = 25; _i < 37; _i++
	{
		_number = BalloonColors[_i][0]
		_name = BalloonColors[_i][1]
		
		if TOINT(CurrentAngelbox[0]) == _i; "\![*]"
		
		"\__q[OnPickColor,%(_number)]%(_name)\__q"
		
		if _i % 2; "\n[0]\f[align,right]"
		else; "\n\f[align,left]"
	}
	
	"\n"
	"\f[align,center]\f[bold,1]Galaxy\f[bold,default]\n\f[align,left]"
	for _i = 37; _i < 43; _i++
	{
		_number = BalloonColors[_i][0]
		_name = BalloonColors[_i][1]
		
		if TOINT(CurrentAngelbox[0]) == _i; "\![*]"
		
		"\__q[OnPickColor,%(_number)]%(_name)\__q"
		
		if _i % 2; "\n[0]\f[align,right]"
		else; "\n\f[align,left]"
	}
	"\n"
	"\f[align,center]"
	"\![*]\__q[OnPickColor,%(RAND(ARRAYSIZE(BalloonColors)))]Random\__q"
	"\n\f[color,default.anchor]Highlighted text\f[color,disable]  Dimmed text\f[color,default]\n\n"
	"\![*]\q[Back,OnConfigMenu]  \![*]\q[Close menu,blank]"
	"\![unlock,balloonrepaint]"
}

OnPickColor
{
	CurrentAngelbox = BalloonColors[reference0]
	OnBalloonCustomization
}

BalloonColors : array
{
	"0,Wings"
	"1,Angel"
	"2,Pilot"
	"3,Halo"
	"4,Red"
	"5,Orange"
	"6,Gold"
	"7,Yellow"
	"8,Chartreuse"
	"9,Green"
	"10,Teal"
	"11,Cyan"
	"12,Sky"
	"13,Blue"
	"14,Indigo"
	"15,Violet"
	"16,Purple"
	"17,Lilac"
	"18,Magenta"
	"19,Pink"
	"20,Tawny"
	"21,Brown"
	"22,Black"
	"23,Gray"
	"24,White"
	"25,Unicorn Fangs"
	"26,Pirate Lich"
	"27,Endless Dawn"
	"28,Cat-tastrophe"
	"29,Cursed"
	"30,Therapy"
	"31,Shortcake"
	"32,Medicine"
	"33,Butterfly"
	"34,Catwitch"
	"35,Ladybug"
	"36,Bridge"
	"37,Spiral Galaxy"
	"38,Purple Nebula"
	"39,Red Nebula"
	"40,Blue Nebula"
	"41,Green Nebula"
	"42,Gray Nebula"
}

OnDebug.UserAcceptedRole
{
	if UserAcceptedRole != 1; UserAcceptedRole = 1
	else; UserAcceptedRole = 0
	OnSakuraMenu
}