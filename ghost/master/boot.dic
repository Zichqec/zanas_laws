OnFirstBoot
{
	stroke = 0
	LastTalk = ""
	UserAcceptedRole = 0
	TotalUniverses = 0
	UserFainted = 0
	LastOrbClick = (0,0)
	CurrentAngelbox = "13,Blue"
	IsFloating = 1
	
	OldQuestions.MoreOrFewer.Animals = IARRAY
	LastQuestions.MoreOrFewer.Animals = ""
	Questions.MoreOrFewer.Animals = ""
	
	OldQuestions.ThisOrThat.Animals = IARRAY
	LastQuestions.ThisOrThat.Animals = ""
	Questions.ThisOrThat.Animals = ""
	
	"\1\s[-1]\0\s[0]Firstboot"
}

OnInitialize
{
	if reference0 != "reload" //To reset EXCEPT when just reloading
	{
		LastTalk = ""
	}
}

OnBoot
{
	_temp = SHIORI3FW.UserBirthday
	_user_birthday = "%(_temp[1])/%(_temp[2])" //M/D, if you want it D/M you can switch it around
	_today = "%(month)/%(day)" //Again, switch this around if using D/M
	
	"\1\s[-1]\0\![set,alpha,0]\s[0]\t%(fadein)"
	--
	if !UserAcceptedRole
	{
		"Ah, you're awake again."
	}
	elseif _today == _user_birthday
	{
		"\0\s[0]Birthday."
	}
	else //Normal boot
	{
		"\0\s[0]Boot"
	}
}

OnClose
{
	if !UserAcceptedRole
	{
		UserFainted = 1
		"Oh, are you alright? You're looking a little faint..."
	}
	else
	{
		"\0\s[0]Close"
	}
	--
	"\w8\w8\t%(fadeout)\-"
}